Ian Ray 4th September 2025

\begin{code}

{-# OPTIONS --safe --without-K #-}

module UF.HedbergDirect where

open import MLTT.Spartan
open import UF.Base

\end{code}

We convert a direct proof of hedberg's lemma, by Carlo Angiuli, which heavily
relies on 'with abstraction' to a proof that only uses 'where'.

(This direct proof is due to Nicolai Kraus see:
https://homotopytypetheory.org/2012/03/30/a-direct-proof-of-hedbergs-theorem/)

\begin{code}

Lemma-Motive : {X : ğ“¤ Ì‡}
             â†’ ((x y : X) â†’ is-decidable (x ï¼ y))
             â†’ (a b : X)
             â†’ (a ï¼ b)
             â†’ ğ“¤ Ì‡
Lemma-Motive {ğ“¤} {X} dec a b p = I (dec a a) (dec a b)
 module Lemma-Motive-helper where
  I : is-decidable (a ï¼ a) â†’ is-decidable (a ï¼ b) â†’ ğ“¤ Ì‡
  I (inl r) (inl q) = p ï¼ r â»Â¹ âˆ™ q
  I (inl _) (inr np) = ğŸ˜-elim (np p)
  I (inr nr) _ = ğŸ˜-elim (nr refl)

Lemma : {X : ğ“¤ Ì‡}
      â†’ (dec : (x y : X) â†’ is-decidable (x ï¼ y))
      â†’ (a b : X) (p : a ï¼ b)
      â†’ Lemma-Motive dec a b p
Lemma {_} {X} dec a a refl = I (dec a a)
 module Lemma-helper where
  I : (d : is-decidable (a ï¼ a)) â†’ Lemma-Motive-helper.I dec a a refl d d
  I (inl r) = sym-is-inverse r
  I (inr nr) = ğŸ˜-elim (nr refl)

hedberg-direct : {X : ğ“¤ Ì‡}
               â†’ (dec : (x y : X) â†’ is-decidable (x ï¼ y))
               â†’ (a b : X)
               â†’ (p q : a ï¼ b)
               â†’ p ï¼ q
hedberg-direct dec a b p q
 = I (dec a a) (dec a b) (Lemma dec a b p) (Lemma dec a b q)
 where
  I : (r : is-decidable (a ï¼ a))
    â†’ (s : is-decidable (a ï¼ b))
    â†’ Lemma-Motive-helper.I dec a b p r s
    â†’ Lemma-Motive-helper.I dec a b q r s
    â†’ p ï¼ q
  I (inl r) (inl s) Î± Î² = Î± âˆ™ Î² â»Â¹
  I (inl r) (inr ns) Î± Î² = ğŸ˜-elim (ns p)
  I (inr nr) _ Î± Î² = ğŸ˜-elim (nr refl)
  

\end{code}

Or we could give all top level definitions
(for whatever reason (;)

\begin{code}

Lemma-Motive-helper' : {X : ğ“¤ Ì‡}
                     â†’ (a b : X)
                     â†’ (a ï¼ b)
                     â†’ is-decidable (a ï¼ a)
                     â†’ is-decidable (a ï¼ b)
                     â†’ ğ“¤ Ì‡
Lemma-Motive-helper' a b p (inl r) (inl s) = p ï¼ r â»Â¹ âˆ™ s
Lemma-Motive-helper' a b p (inl _) (inr ns) = ğŸ˜-elim (ns p)
Lemma-Motive-helper' a b p (inr nr) _ = ğŸ˜-elim (nr refl)

Lemma-Motive' : {X : ğ“¤ Ì‡}
              â†’ ((x y : X) â†’ is-decidable (x ï¼ y))
              â†’ (a b : X)
              â†’ (a ï¼ b)
              â†’ ğ“¤ Ì‡
Lemma-Motive' dec a b p = Lemma-Motive-helper' a b p (dec a a) (dec a b)

Lemma-helper' : {X : ğ“¤ Ì‡}
              â†’ (a : X)
              â†’ (d : is-decidable (a ï¼ a))
              â†’ Lemma-Motive-helper' a a refl d d
Lemma-helper' a (inl r) = sym-is-inverse r
Lemma-helper' a (inr nr) = ğŸ˜-elim (nr refl)

Lemma' : {X : ğ“¤ Ì‡}
       â†’ (dec : (x y : X) â†’ is-decidable (x ï¼ y))
       â†’ (a b : X) (p : a ï¼ b)
       â†’ Lemma-Motive' dec a b p
Lemma' dec a a refl = Lemma-helper' a (dec a a)

hedberg-helper' : {X : ğ“¤ Ì‡}
                â†’ (a b : X)
                â†’ (p q : a ï¼ b)
                â†’ (r : is-decidable (a ï¼ a))
                â†’ (s : is-decidable (a ï¼ b))
                â†’ Lemma-Motive-helper' a b p r s
                â†’ Lemma-Motive-helper' a b q r s
                â†’ p ï¼ q
hedberg-helper' a b p q (inl r) (inl s) Î± Î² = Î± âˆ™ Î² â»Â¹
hedberg-helper' a b p q (inl r) (inr ns) Î± Î² = ğŸ˜-elim (ns p)
hedberg-helper' a b p q (inr nr) _ Î± Î² = ğŸ˜-elim (nr refl)

hedberg-direct' : {X : ğ“¤ Ì‡}
                â†’ (dec : (x y : X) â†’ is-decidable (x ï¼ y))
                â†’ (a b : X)
                â†’ (p q : a ï¼ b)
                â†’ p ï¼ q
hedberg-direct' dec a b p q
 = hedberg-helper' a b p q (dec a a) (dec a b)
    (Lemma' dec a b p) (Lemma' dec a b q)

\end{code}
